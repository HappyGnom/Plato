plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 31

    defaultConfig {
        applicationId "by.happygnom.plato"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders.crashlyticsCollectionEnabled = false
            manifestPlaceholders.analyticsCollectionEnabled = false

            versionNameSuffix "-D"
            signingConfig signingConfigs.release
        }

        release {
            manifestPlaceholders.crashlyticsCollectionEnabled = true
            manifestPlaceholders.analyticsCollectionEnabled = true

            versionNameSuffix "-R"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'by.happygnom.plato'

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Plato_${variant.buildType.name}_${defaultConfig.versionName}.apk"
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.appcompat:appcompat:1.4.2"
    implementation "com.google.android.material:material:1.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"

    implementation project(path: ':domain')
    implementation project(path: ':data')

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.1')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-perf'

    implementation 'com.firebaseui:firebase-ui-auth:4.3.2'
    implementation 'com.google.android.gms:play-services-auth:20.2.0'

    // Jetpack Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.activity:activity-compose:1.4.0"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0-alpha02"

    // ViewModel + LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Navigation
    implementation "androidx.navigation:navigation-compose:2.5.0-rc02"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Permissions
    implementation "com.google.accompanist:accompanist-permissions:0.15.0"

    // Coil Image Loading
    implementation "io.coil-kt:coil-compose:1.4.0"

    // Collapsing toolbar
    implementation "me.onebone:toolbar-compose:2.3.0"

    // Insets
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets-ui:$accompanist_version"

    // Permissions
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"

    // SwipeRefresh
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"

    // Pager
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"

    // Coroutines play-services adapter
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.1"

}
